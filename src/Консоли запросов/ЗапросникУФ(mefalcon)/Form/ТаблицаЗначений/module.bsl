Перем РедактируемаяКолонкаИмя; //переменная для хранения имени колонки, которую начали редактировать

Процедура ПриОткрытии()
	
	ЭлементыФормы.ТаблицаЗначений.СоздатьКолонки();
	
	НастройкаКолонок.Очистить();
	
	Для каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		НовСтрока = НастройкаКолонок.Добавить();
		НовСтрока.ИмяКолонки = Колонка.Имя;
		НовСтрока.ТипЗначенияКолонки = Колонка.ТипЗначения; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура КнопкаСохранитьНажатие(Элемент)
	
	Закрыть(ТаблицаЗначений.Скопировать());
	
КонецПроцедуры

Процедура КнопкаЗакрытьОкноНажатие(Элемент)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

Процедура КоманднаяПанельДействияНастройкаКолонок(Кнопка)
	
	ЭлементыФормы.ПанельНастройкаКолонок.Видимость = Истина;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////
/// НАСТРОЙКА КОЛОНОК --->                                                

Процедура НастройкаКолонокПередУдалением(Элемент, Отказ)
	
	ИмяКолонки = ЭлементыФормы.НастройкаКолонок.ТекущиеДанные.ИмяКолонки;
	
	ТекстЗаголовка = "Запрос на подтверждение удаления колонки";
	ТекстСообщения = "Вы действительно хотите удалить колонку """ + ИмяКолонки + """?";
	
	Ответ = Вопрос(ТекстСообщения, РежимДиалогаВопрос.ДаНет, , , ТекстЗаголовка);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ИндексТекКолонки = ТаблицаЗначений.Колонки.Найти(ЭлементыФормы.НастройкаКолонок.ТекущиеДанные.ИмяКолонки); 
	
	ТаблицаЗначений.Колонки.Удалить(ИндексТекКолонки);
	ЭлементыФормы.ТаблицаЗначений.СоздатьКолонки();
	
КонецПроцедуры

Процедура НастройкаКолонокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекДанные = ЭлементыФормы.НастройкаКолонок.ТекущиеДанные; 
	
	Если ПустаяСтрока(ТекДанные.ИмяКолонки) ИЛИ ТекДанные.ТипЗначенияКолонки = Новый ОписаниеТипов Тогда
		//недозаполненное описание колонки
		Возврат;
	КонецЕсли; 
	
	РедактируемаяКолонка = ТаблицаЗначений.Колонки.Найти(РедактируемаяКолонкаИмя);
	
	Если РедактируемаяКолонка = Неопределено Тогда
		
		//добавили или дозаполнили новую колонку
		
		ТаблицаЗначений.Колонки.Добавить(ТекДанные.ИмяКолонки, ТекДанные.ТипЗначенияКолонки, ТекДанные.ИмяКолонки);
		
	Иначе
		
		//изменили имеющуюся
		
		Если НЕ (РедактируемаяКолонка.ТипЗначения = Новый ОписаниеТипов(ТекДанные.ТипЗначенияКолонки)) Тогда
			ТаблицаЗначений.Колонки.Удалить(РедактируемаяКолонка);
			ТаблицаЗначений.Колонки.Добавить(ТекДанные.ИмяКолонки, ТекДанные.ТипЗначенияКолонки, ТекДанные.ИмяКолонки);
		Иначе			
			РедактируемаяКолонка.Имя         = ТекДанные.ИмяКолонки;
			РедактируемаяКолонка.Заголовок   = ТекДанные.ИмяКолонки;
		КонецЕсли;	   
		
	КонецЕсли; 
	
	ЭлементыФормы.ТаблицаЗначений.СоздатьКолонки();
	
КонецПроцедуры

Процедура НастройкаКолонокПередНачаломИзменения(Элемент, Отказ)
	
	//запомним колонку, которую начали менять
	РедактируемаяКолонкаИмя = ЭлементыФормы.НастройкаКолонок.ТекущиеДанные.ИмяКолонки;
	
КонецПроцедуры

Процедура НастройкаКолонокИмяКолонкиПриИзменении(Элемент)
	
	ТекДанные = ЭлементыФормы.НастройкаКолонок.ТекущиеДанные; 
	
	ТекДанные.ИмяКолонки = СтрЗаменить(ТекДанные.ИмяКолонки," ","");
	ТекДанные.ИмяКолонки = СокрЛП(ТекДанные.ИмяКолонки);
	
КонецПроцедуры

Процедура КнопканНастройкаКолонокЗакрытьНажатие(Элемент)
	
	ЭлементыФормы.ПанельНастройкаКолонок.Видимость = Ложь;	
	
КонецПроцедуры//КнопканНастройкаКолонокЗакрытьНажатие

// <--- НАСТРОЙКА КОЛОНОК                                                                   
////////////////////////////////////////////////////////////////////////// 
